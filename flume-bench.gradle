apply plugin: 'groovy'

repositories {
  jcenter()
}

ext.flumeVersion = '1.5.2'

// In this section you declare the dependencies for your production and test code
dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.0:indy'

  compile 'org.slf4j:slf4j-api:1.7.7'
  compile 'ch.qos.logback:logback-classic:1.1.2'
  compile 'net.sourceforge.argparse4j:argparse4j:0.4.4'
  compile "org.apache.flume:flume-ng-sdk:$flumeVersion"
  compile 'com.google.guava:guava:18.0'
  compile 'io.dropwizard:dropwizard-util:0.7.1'
  compile 'io.dropwizard.metrics:metrics-core:3.1.0'

  testCompile 'junit:junit:4.11'
  testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

tasks.withType(GroovyCompile) {
  groovyOptions.optimizationOptions.indy = true
}


task downloadFlume(type:Copy) {
  def filename = "apache-flume-${flumeVersion}-bin.tar.gz"

  from tarTree("$buildDir/$filename")
  into "$buildDir/flume"

  doFirst {
    def location = "flume/${flumeVersion}/${filename}"
    def urls = [
        "http://mirror.catn.com/pub/apache/$location",
        "http://mirrors.ukfast.co.uk/sites/ftp.apache.org/$location",
        "http://mirror.ox.ac.uk/sites/rsync.apache.org/$location",
        "http://apache.mirror.anlx.net/$location",
        "http://mirror.vorboss.net/apache/$location",
        "http://mirror.gopotato.co.uk/apache/$location",
        "http://www.mirrorservice.org/sites/ftp.apache.org/$location"
    ]
    Collections.shuffle(urls)
    logger.lifecycle("downloading: ${urls.first()}")
    ant.get(src: urls.first(), dest: buildDir)
  }
}

